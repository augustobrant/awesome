/** Configura CET Source Copyright Notice (CETSC)

   This file contains Configura CM source code and is part of the
   Configura CET Development Platform (CETDEV). Configura CM
   is a programming language created by Configura Sverige AB.
   Configura, Configura CET and Configura CM are trademarks of
   Configura Sverige AB. Configura Sverige AB owns Configura CET,
   Configura CM, and CETDEV.

   Copyright (C) 2004 Configura Sverige AB, All rights reserved.

   You can modify this source file under the terms of the Configura CET
   Source Licence Agreement (CETSL) as published by Configura Sverige AB.

   Configura Sverige AB has exclusive rights to all changes, modifications,
   and corrections of this source file. Configura Sverige AB has exclusive
   rights to any new source file containing material from this source file.
   A new source file based on this source file or containing material from
   this source file has to include this Configura CET Source Copyright Notice
   in its full content. All changes, modifications, and corrections mentioned
   above shall be reported to Configura Sverige AB within One Month from
   the date that the modification occurred.

   Configura CM is distributed in the hope that it will be useful, but
   WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
   See the CETSL for more details.

   You should have received a copy of the CETSL along with the CETDEV.
   If not, write to Configura Sverige AB, Box 306, SE-581 02 Link√∂ping, Sweden.
   Tel +46 13377800, Fax +46 13377855,
   Email: info@configura.com, www.configura.com

   END OF CETSC
*/

package custom.awesome.electrical;

public class AweElectricalStatus {
    
    private AweElectricalMap map;
    public bool activeOnly = true;

    public AweElectricalCluster[] clusters();

    public constructor(AweElectricalMap map, bool activeOnly=true) {
        this.map = map;
        this.activeOnly = activeOnly;
        updateStatus();
    }   

    extend public void updateStatus() {
        identifyClusters(map);
    }

    extend public Primitive3D get3D() {
        Awe3D all3D();
        for(cluster in clusters) {
            Awe3D cluster3D();
            for(cable in cluster.cables) {
                var cableLine = cable.spaceLine.v;
                Cylinder3D l(cableLine.p0, cableLine.p1, .5inch);
                cluster3D << l;
            }
            cluster3D.setMaterial(cluster.infeeds.count != 1 ? plainRedMaterial3D : plainGreenMaterial3D);
            all3D << cluster3D;
        }
        return all3D;
    }

    extend private void identifyClusters(AweElectricalMap map) {
        AweElectricalInfeedPoint[] infeeds();
        AweElectricalCableLine[] cables();
        if(activeOnly) {
            for(i in map.allInfeeds) {
                if(i.active) infeeds << i;
            }
            for(c in map.allCables) {
                if(c.active) cables << c;
            }
        }
        else {
            infeeds = map.allInfeeds;
            cables = map.allCables;
        }

        clusters.clear();
        point[] visitedPoints();

        var index = indexPointsToLines(cables);

        for(infeed in infeeds) {
            identifyAndAddInfeedCluster(clusters, infeed, visitedPoints, index);
        }
    }

    extend private void identifyAndAddInfeedCluster(AweElectricalCluster[] clusters, AweElectricalInfeedPoint infeed, point[] visitedPoints, str->AweElectricalCableLine[] pointLineIndex) {
        point p = infeed.spacePoint.v;

        //aready in someone else's cluster
        if(p in visitedPoints) {
            return;
        }

        var cables = identifyCluster(p, visitedPoints, pointLineIndex);
        AweElectricalCluster cluster();
        cluster.cables = cables;

        clusters << cluster;

    }

    extend private AweElectricalCableLine[] identifyCluster(point p, point[] visitedPoints, str->AweElectricalCableLine[] pointLineIndex, AweElectricalCableLine[] visitedLines=new AweElectricalCableLine[]()) {

        str pointStr = p.toS;
        var lines = pointLineIndex.get(pointStr);
        for(l in lines) {
            if(l !in visitedLines) {
                visitedLines << l;
                identifyCluster(l.spaceLine.v.p0, visitedPoints, pointLineIndex, visitedLines);
                identifyCluster(l.spaceLine.v.p1, visitedPoints, pointLineIndex, visitedLines);
            }
        }
        if(p !in visitedPoints) {
            visitedPoints << p;
        }
        return visitedLines;
    }

    extend private str->AweElectricalCableLine[] indexPointsToLines(AweElectricalCableLine[] cables) {

        str->AweElectricalCableLine[] pointLineIndex();

        for(cable in cables) {
            str pointStr = cable.spaceLine.v.p0.toS;
            if(!pointLineIndex.get(pointStr)) {
                pointLineIndex.put(pointStr, new AweElectricalCableLine[]());
            }
            pointLineIndex.get(pointStr) << cable;

            pointStr = cable.spaceLine.v.p1.toS;
            if(!pointLineIndex.get(pointStr)) {
                pointLineIndex.put(pointStr, new AweElectricalCableLine[]());
            }
            pointLineIndex.get(pointStr) << cable;
        }

        return pointLineIndex;
    }
}