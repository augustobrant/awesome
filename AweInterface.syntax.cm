/** Configura CET Source Copyright Notice (CETSC)

   This file contains Configura CM source code and is part of the
   Configura CET Development Platform (CETDEV). Configura CM
   is a programming language created by Configura Sverige AB.
   Configura, Configura CET and Configura CM are trademarks of
   Configura Sverige AB. Configura Sverige AB owns Configura CET,
   Configura CM, and CETDEV.

   Copyright (C) 2004 Configura Sverige AB, All rights reserved.

   You can modify this source file under the terms of the Configura CET
   Source Licence Agreement (CETSL) as published by Configura Sverige AB.

   Configura Sverige AB has exclusive rights to all changes, modifications,
   and corrections of this source file. Configura Sverige AB has exclusive
   rights to any new source file containing material from this source file.
   A new source file based on this source file or containing material from
   this source file has to include this Configura CET Source Copyright Notice
   in its full content. All changes, modifications, and corrections mentioned
   above shall be reported to Configura Sverige AB within One Month from
   the date that the modification occurred.

   Configura CM is distributed in the hope that it will be useful, but
   WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
   See the CETSL for more details.

   You should have received a copy of the CETSL along with the CETDEV.
   If not, write to Configura Sverige AB, Box 306, SE-581 02 Link√∂ping, Sweden.
   Tel +46 13377800, Fax +46 13377855,
   Email: info@configura.com, www.configura.com

   END OF CETSC
*/

package custom.awesome;
use cm.syntax;

public class AweTypeBundle : abstract {
    extend public Type[] types() : abstract { }
    public constructor() {
    }   
}

public statement aweCast @what=id "as" @bundle=type @code=statement {
    AweTypeBundle mc = bundle.type.Class.instantiate.AweTypeBundle;
    SStatements s = statement {  };
    
    for (t in mc.types) { 
        SType tType(t);
        s << block { 
            if (@what.@tType) { 
                @tType @what = @what.@tType;
                @code;
            }
        };
    }
    
    return s;
}


public definition @visibility aweTypeBundle @name=id '<' @types=list[type, ',']+ '>' ';' {
    Type[] list();
    SStatements s();
    for(t in types) {
        s << block { list << @t; };
    }

    definition {
        @visibility class @name extends AweTypeBundle {
            public Type[] types() {
                Type[] list();
                @s;
                return list;
            }
        }
    };
}

public class X {
    extend public void pln() {
        pln("eh o xis");
    }
    public constructor() { }
}

public class Y {
    extend public void pln() {
        pln("eh o ips");
    }
    public constructor() { }
}

public class Factory {
    extend public Object get(str type) {
        if(type == "x")
            return X();
        else 
            return Y();
    }
    public constructor() { }
}

public aweTypeBundle IXY<X,Y>;

{
    Object o = Factory().get("x");
    
    aweCast o as IXY {
        o.pln();
    }
}